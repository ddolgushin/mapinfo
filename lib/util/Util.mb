Include "mapbasic.def"
Include "Util.def"
Include "Array.def"


Define SELF_ANCHOR			"$0"


Global gl_timer As Integer


Declare Function CompareStringPart(ByVal s As String, ByVal what As String,
	ByVal head As Logical) As Logical


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''
'' INTERFACE IMPLEMENTATION
''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


'
' Replace all pattern occurences in source string.
'
Function StrReplace(ByVal pattern As String, ByVal toWhat As String,
		ByVal source As String) As String
	Dim pos As Integer
	Dim res As String
	
	Do
		pos = InStr(1, source, pattern)
		
		If pos > 0 Then
			res = res + Left$(source, pos - 1) + toWhat
			source = Right$(source, Len(source) - pos - Len(pattern) + 1)
		End If
	Loop While pos > 0
	
	StrReplace = res + source
End Function


'
' Get substring consisting of wordCount words.
'
Function GetWords(ByVal srcStr As String, ByVal wordCount As Integer) As String
	Dim res As String
	Dim chunks() As String
	Dim i, size As Integer
	
	Call Split(srcStr, " ", chunks)
	
	size = UBound(chunks)
	
	If size > wordCount Then
		size = wordCount
	End If
	
	For i = 1 To size
		res = res + chunks(i)
		
		If i < size Then
			res = res + " "
		End If
	Next
	
	GetWords = res
End Function


'
' Count a number of given word occurences in srcStr.
'
' If delimBySpace is true, only entries separated from other content
' by whitespace (or tab) are counted.
'
Function CountWords(ByVal srcStr As String, ByVal word As String,
		ByVal delimBySpace As Logical) As Integer
	Dim res, pos, wordLen, srcLen As Integer
	Dim tail As String
	
	srcLen = Len(srcStr)
	wordLen = Len(word)
	tail = " "
	
	If delimBySpace And srcLen > wordLen Then
		srcStr = StrReplace("	", " ", srcStr) + tail ' Replace tabs to whitespaces.
		pos = InStr(1, srcStr, word + tail)
	Else
		pos = InStr(1, srcStr, word)
	End If
	
	Do While pos > 0
		res = res + 1
		
		If delimBySpace And (pos + 2 * (wordLen + 1)) <= srcLen Then
			pos = InStr(pos + wordLen + 1, srcStr, word + tail)
		Else
			pos = InStr(pos + wordLen, srcStr, word)
		End If
	Loop
	
	CountWords = res
End Function


'
' Return string presenting roung format, e.g. 0.001 for 15.32548 => 15.326.
'
Function GetRoundFormat(ByVal prec As SmallInt) As Float
	Dim res As Float
	Dim tmpStr As String
	
	If prec > 0 Then
		tmpStr = "0." + String$(prec - 1, "0") + "1"
	Else
		tmpStr = "1"
	End If
	
	res = StrToFloat(tmpStr)
	
	GetRoundFormat = res
End Function


'
' Split src string into chunks using given delimiter.
'
Sub Split(ByVal src As String, ByVal delimiter As String, chunks() As String)
	Call SplitExt(src, delimiter, chunks, FALSE)
End Sub


Sub SplitExt(ByVal src As String, ByVal delimiter As String,
		chunks() As String, ByVal trimChunks As Logical)
	Dim idx, srcLen, cutLen As Integer
	Dim pos1, pos2 As Integer
	Dim size As Integer
	
	ReDim chunks(0)
	
	If Len(delimiter) > 0 Then
		pos1 = 1
		pos2 = InStr(pos1, src, delimiter)
		
		If pos2 > 0 Then
			' If delimiter is encountered at position 1 - add "empty" value
			' and recalculate pos1 and pos2
			If pos2 = 1 Then
				ReDim chunks(1)
				
				chunks(1) = ""
				
				pos1 = 2
				pos2 = InStr(pos1, src, delimiter)
			End If
			
			srcLen = Len(src)
			
			If UBound(chunks) = 0 Then idx = 1
			Else idx = 2 End If
			
			Do While pos2 <> 0
				ReDim chunks(idx)
				
				If pos2 - pos1 <> 0 Then
					If StringCompare(Mid$(src, pos1, 1), delimiter) <> 0 Then
						cutLen = pos2 - pos1
					Else
						cutLen = 0
					End If
				Else
					If StringCompare(Mid$(src, pos1, 1), delimiter) = 0 Then
						cutLen = 0
					End If
				End If
				
				chunks(idx) = Mid$(src, pos1, cutLen)
				
				If trimChunks Then chunks(idx) = Trim(chunks(idx)) End If
				
				If pos2 < srcLen Then
					pos1 = pos2 + 1
					pos2 = InStr(pos1, src, delimiter)
				Else
					If pos1 < srcLen And pos2 = 0 Then
						pos1 = pos2
					Else
						If StringCompare(Right$(src, 1), delimiter) = 0 Then
							size = UBound(chunks) + 1
							
							ReDim chunks(size)
							
							chunks(size) = ""
						End If
						
						Exit Do
					End If
				End If
				
				idx = idx + 1
				
				If pos2 = 0 Then pos2 = srcLen + 1 End If
				If pos1 > srcLen Then Exit Do End If
			Loop
		ElseIf Len(src) > 0 Then
			ReDim chunks(1)
			
			chunks(1) = src
			
			If trimChunks Then chunks(1) = Trim(chunks(1)) End If
		End If
	End If
End Sub


'
' Split source string into chunks of given length.
'
Sub SplitByLen(ByVal src As String, ByVal chunkLen As Integer,
		chunks() As String)
	Dim i, size As SmallInt
	Dim length, pos, curChunkLen As Integer
	Dim chunk As String
	
	ReDim chunks(0)
	
	length = Len(src)
	
	If chunkLen > length Then
		Call AddStringToArray(chunks, src)
	Else
		pos = 1
		size = length / chunkLen
		
		If length Mod chunkLen > 0 Then
			size = size + 1
		End If
		
		For i = 1 To size
			If i < size Then
				curChunkLen = chunkLen
			Else
				curChunkLen = length - (size - 1) * chunkLen
			End If
			
			chunk = Mid$(src, pos, curChunkLen)
			pos = pos + curChunkLen
			
			If Len(chunk) > 0 Then
				Call AddStringToArray(chunks, chunk)
			End If
		Next
	End If
End Sub


'
' Join chunks into a single string using glue.
'
Function Join(chunks() As String, ByVal glue As String) As String
	Join = JoinExt(chunks, glue, FALSE)
End Function


Function JoinExt(chunks() As String, ByVal glue As String, ByVal skipEmpty As Logical) As String
	Dim size, i As Integer
	Dim res As String
	Dim substSelf As Logical
	
	size = UBound(chunks)
	substSelf = StrContains(glue, SELF_ANCHOR)

	If size = 0 Then Exit Sub End If
	
	For i = 1 To size
		If Not skipEmpty Or (skipEmpty And Len(chunks(i)) > 0) Then
			If i < size Then
				If Not substSelf Then res = res + chunks(i) + glue
				Else res = res + chunks(i) + StrReplace(SELF_ANCHOR, chunks(i), glue) End If
			Else
				If Not substSelf Then res = res + chunks(i)
				Else res = res + chunks(i) + Mid$(glue, 1, InStr(1, glue, SELF_ANCHOR) - 1) + chunks(i) End If
			End If
		End If
	Next

	res = Trim(res)

	If skipEmpty And Len(res) > 0 And EndsWith(res, ",") Then
		res = Trim(Mid$(res, 1, Len(res) - 1))
	End If

	JoinExt = res
End Function


'
' Replace all $-prefixed values on those given in repls().
'
Function ReplaceAnchors(ByVal src As String, ByVal anchorPrefix As String,
		repls() As String) As String
	Dim size As Integer
	Dim i, pos As Integer
	Dim anchor As String

	size = UBound(repls)
	
	If size = 0 Then
		Exit Sub
	End If
	
	For i = 1 To size
		anchor = anchorPrefix + i
		pos = InStr(1, src, anchor)
		
		Do While pos <> 0
			src = Mid$(src, 1, pos - 1) + repls(i)
					+ Mid$(src, pos + Len(anchor), Len(src) - pos + Len(anchor))
			pos = InStr(pos + Len(anchor), src, anchor)
		Loop
	Next
	
	ReplaceAnchors = src
End Function


'
' Wrap string to given lengts, braking each line with ASCII 13th symbol.
'
Function WrapString(ByVal src As String, ByVal lineLen As Integer,
		ByVal trimLines As Logical) As String
	Dim pos, lastSpacePos As Integer
	Dim curChar, tmpStr, lines() As String
	
	pos = 1
	
	Do While pos <= Len(src)
		tmpStr = ""
		
		If pos >= lineLen Then
			If lastSpacePos > 0 Then
				tmpStr = Mid$(src, 1, lastSpacePos)
				src = Mid$(src, lastSpacePos, Len(src) - lastSpacePos + 1)
			Else
				If lineLen > Len(src) Then
					tmpStr = Mid$(src, 1, Len(src))
				Else
					tmpStr = Mid$(src, 1, lineLen)
				End If
				
				src = Mid$(src, lineLen, Len(src) - lineLen + 1)
			End If
			
			pos = 1
			lastSpacePos = 0
		ElseIf pos = Len(src) Then
			tmpStr = src
		End If
		
		If Len(tmpStr) > 0 Then
			If trimLines = 1 Then
				tmpStr = LTrim$(RTrim$(tmpStr))
			End If
			
			Call AddStringToArray(lines, tmpStr)
		End If
		
		curChar = Mid$(src, pos, 1)
		
		If StringCompare(curChar, " ") = 0 Then
			lastSpacePos = pos
		End If
		
		pos = pos + 1
	Loop
	
	WrapString = Join(lines, Chr$(13))
End Function


'
' Trim string from both sides.
'
Function Trim(ByVal v As String) As String
	Trim = LTrim$(RTrim$(v))
End Function


'
' Convert float to string with default two decimals rounding.
'
Function FloatToStr(ByVal v As Float) As String
	FloatToStr = StrReplace(".", ",", Str$(v))
End Function


'
' Convert float to string using given format, e.g. FloatToStrCustom(12.3, "#.##")
'
Function FloatToStrCustom(ByVal v As Float, ByVal fmt As String) As String
	FloatToStrCustom = StrReplace(".", ",", Format$(v, fmt))
End Function


'
' Convert string to float.
'
Function StrToFloat(ByVal v As String) As Float
	v = DeformatNumber$(v)
	v = StrReplace(",", ".", v)
	
	StrToFloat = Val(v)
End Function


Function PrepareFloat(ByVal v As Float) As String
	PrepareFloat = PrepareFloatExt(v, DEFAULT_FLOAT_PREC, FALSE)
End Function


Function PrepareFloatExt(ByVal v As Float, ByVal decimalLength As Integer,
		ByVal separateWithComma As Logical) As String
	Dim res As String
	
	res = Format$(v, "0." + String$(decimalLength, "0"))
	res = StrReplace(" ", "", res)
	
	If separateWithComma Then res = StrReplace(".", ",", res)
	Else res = StrReplace(",", ".", res) End If
	
	PrepareFloatExt = res
End Function


Function StrToInt(ByVal v As String) As Integer
	StrToInt = Int(StrToFloat(v))
End Function


Function LogicalToInt(ByVal v As Logical) As SmallInt
	Dim res As SmallInt
	
	If v Then res = 1
	Else res = 0 End If
	
	LogicalToInt = res
End Function


'
' Convert string representation logical to system one.
'
Function StrToLogical(ByVal v As String) As Logical
	Dim res As Logical
	
	v = Trim(v)
	
	If StrToInt(v) = 1 Or UCase$(v) = "T" Or UCase$(v) = "TRUE" Then
		res = TRUE
	End If
	
	StrToLogical = res
End Function


Function StartsWith(ByVal s As String, ByVal what As String) As Logical
	StartsWith = CompareStringPart(s, what, TRUE)
End Function


Function EndsWith(ByVal s As String, ByVal what As String) As Logical
	EndsWith = CompareStringPart(s, what, FALSE)
End Function


'
' If head is true, do like standard StartsWith, else EndsWith.
'
Function CompareStringPart(ByVal s As String, ByVal what As String,
		ByVal head As Logical) As Logical
	Dim res As Logical
	Dim lenWhat As Integer
	
	lenWhat = Len(what)
	
	If Len(s) > lenWhat Then
		If head Then
			res = (StringCompare(Left$(s, lenWhat), what) = 0)
		Else
			res = (StringCompare(Right$(s, lenWhat), what) = 0)
		End If
	ElseIf Len(s) = lenWhat Then
		res = (StringCompare(s, what) = 0)
	End If
	
	CompareStringPart = res
End Function


Function StrContains(ByVal s As String, ByVal subj As String) As Logical
	StrContains = (InStr(1, s, subj) > 0)
End Function


'
' Expects size of format: "0000(M|K|B)" (mega-, kilobytes, and bytes).
'
Function FileSizeToBytes(ByVal sizeStr As String) As Integer
	Dim res, factor As Integer
	
	sizeStr = UCase$(sizeStr)
	
	If EndsWith(sizeStr, "B") Then
		factor = 1
	ElseIf EndsWith(sizeStr, "K") Then
		factor = 1024
	ElseIf EndsWith(sizeStr, "M") Then
		factor = 1024 * 1024
'	ElseIf EndsWith(sizeStr, "G") Then
'		factor = 1024 * 1024 * 1024
'	ElseIf EndsWith(sizeStr, "T") Then
'		factor = 1024 * 1024 * 1024 * 1024
	Else
		factor = 0
	End If
	
	sizeStr = Mid$(sizeStr, 1, Len(sizeStr) - 1)
	res = StrToInt(sizeStr)
	res = res * factor
	
	FileSizeToBytes = res
End Function


Sub PrintArray(arr() As String)
	Dim i, size As Integer
	
	size = UBound(arr)
	
	For i = 1 To size
		Print arr(i)
	Next
End Sub


Sub PrintArrayExt(arr() As String, ByVal prefix As String,
		ByVal suffix As String)
	Dim i, size As Integer
	
	size = UBound(arr)
	
	For i = 1 To size
		Print prefix + arr(i) + suffix
	Next
End Sub


Function ParseFloat(ByVal v As String, out As Float) As Logical
	Dim res As Logical
	
	res = (IsNumber(v) Or Len(v) = 0)
	
	If res Then
		v = StrReplace(",", ".", v)
		v = StrReplace(Chr$(160), "", v)
		out = Val(StrReplace(Chr$(32), "", v))
	End If
	
	ParseFloat = res
End Function


Function ParseInt(ByVal v As String, out As Integer) As Logical
	Dim res As Logical
	Dim i, size As Integer
	
	size = Len(v)
	res = (size > 0)
	
	For i = 1 To size
		If Not IsNumber(Mid$(v, i, 1)) Then
			res = FALSE
			
			Exit For
		End If
	Next
	
	If res Then
		v = StrReplace(Chr$(160), "", v)
		out = Int(StrReplace(Chr$(32), "", v))
	End If
	
	ParseInt = res
End Function


'
' Returns true if string starts with some digit.
'
Function IsNumber(ByVal v As String) As Logical
	Dim res As Logical
	Dim code As SmallInt
	
	If Len(v) > 0 Then
		code = Asc(v)
		res = (code >= 48 And code <= 57)
	End If
	
	IsNumber = res
End Function


Function IsAlpha(ByVal v As String) As Logical
	Dim res As Logical
	Dim code As SmallInt
	
	If Len(v) > 0 Then
		code = Asc(v)
		res = ((code >= 65 And code <= 90) ' A-Z
			Or (code >= 97 And code <= 122) ' a-z
			Or code = 184 ' "¸"
			Or (code >= 224 And code <= 255) ' à-ÿ
			Or (code >= 192 And code <= 223)) ' À-ß
	End If
	
	IsAlpha = res
End Function


'
' Return non-empty string s1 or s2. If both are filled, return first.
'
' NOTE: strings are trimmed.
'
Function GetNonEmptyStr(ByVal s1 As String, ByVal s2 As String) As String
	Dim res As String
	
	s1 = Trim(s1)
	s2 = Trim(s2)	
	res = s1
	
	If Len(res) = 0 Then
		res = s2
	End If
	
	GetNonEmptyStr = res
End Function


Function GetNonZeroInt(ByVal v1 As Integer, ByVal v2 As Integer) As Integer
	Dim res As String
	
	If v1 = 0 Then
		res = v2
	Else
		res = v1
	End If
	
	GetNonZeroInt = res
End Function


Function GetNonZeroFloat(ByVal v1 As Float, ByVal v2 As Float) As Float
	Dim res As String
	
	If v1 = 0 Then
		res = v2
	Else
		res = v1
	End If
	
	GetNonZeroFloat = res
End Function


'
' Join 2 same-sized arrays with glue.
'
Sub JoinArrays(arr1() As String, arr2() As String, ByVal glue As String, arrRes() As String)
	Dim i, size1, size2 As Integer
	Dim tmpArr1(), tmpArr2() As String
	
	Call CopyStringArray(arr1, tmpArr1)
	Call CopyStringArray(arr2, tmpArr2)
	
	size1 = UBound(arr1)
	size2 = UBound(arr2)
	
	ReDim arrRes(0)
	
	If size1 = size2 Then
		For i = 1 To size1
			Call AddStringToArray(arrRes, tmpArr1(i) + glue + tmpArr2(i))
		Next
	End If
End Sub


Sub SwapS(s1 As String, s2 As String)
	Dim tmpStr As String
	
	tmpStr = s1
	s1 = s2
	s2 = tmpStr
End Sub


'
' Cut string from both sides.
'
Function Cut(ByVal str As String, ByVal trimLeft As Integer,
		ByVal trimRight As Integer) As String
	Dim res As String
	Dim totalTrim, strLen As Integer
	
	totalTrim = trimLeft + trimRight
	
	If totalTrim > 0 Then
		strLen = Len(str)
		
		If totalTrim >= strLen Then
			res = ""
		Else
			res = Mid$(str, trimLeft + 1, Len(str) - totalTrim)
		End If
	End If
	
	Cut = res
End Function


Sub TimerReset()
	gl_timer = Timer()
End Sub


Function TimerGetDiff() As Integer
	TimerGetDiff = Timer() - gl_timer
End Function
